buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	
	dependencies {
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
	}
}


apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

rootProject.version = '0.2.0'
group = 'com.github.gnosticoccultist'

repositories {
	jcenter()
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

javadoc {
	failOnError = false
}

dependencies {
	
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
	testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.2"
}

test {
	useJUnitPlatform()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			version = rootProject.version
			afterEvaluate {
				artifactId = 'alchemy-utilities'
				groupId = rootProject.group
			}
		}
	}
}

artifactory {
	contextUrl = "${artifactory_contextUrl}"
	publish {
        repository {
            repoKey = 'default-maven-virtual'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
            
        }
        defaults {
			// Reference to Gradle publications defined in the build script.
			// This is how we tell the Artifactory Plugin which artifacts should be
			// published to Artifactory.
			publications('mavenJava')
			publishArtifacts = true
			// Properties to be attached to the published artifacts.
			properties = ['qa.level': 'basic', 'dev.team' : 'core']
			publishPom = true // Publish generated POM files to Artifactory (true by default)
    	}
    }
}